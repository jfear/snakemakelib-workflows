# -*- snakemake -*-
import re
import pandas as pd
from snakemakelib.applications import summarize_expression_data
from snakemakelib_workflows.scrnaseq import scrnaseq_pca, scrnaseq_qc, _save_align_rseqc_metrics

##############################
# Additional merge rule for transcript alignment files
##############################
def _find_transcript_bam(wildcards):
    tgt = IOTarget(config['settings']['sample_organization'].run_id_re.file,
                   suffix=_merge_tx_suffix(config['bio.ngs.settings']['aligner']))
    m = config['settings']['sample_organization'].sample_re.match(wildcards.prefix)
    return [src for src in make_targets(tgt_re=tgt, samples=_samples) if dirname(src).startswith(m.groupdict()['SM'])]

rule scrnaseq_picard_merge_sam_transcript:
    """scrnaseq picard: merge sam files from transcript alignments.

    NB: always outputs bam files!
    """
    params: cmd = config['bio.ngs.qc.picard']['cmd'] + "MergeSamFiles",
            options = " ".join([config['bio.ngs.qc.picard']['options'],
                                config['bio.ngs.qc.picard']['merge_sam']['options']])
    input: _find_transcript_bam
    output: merge="{prefix}.merge.tx.bam"
    run: 
      if (len(input) > 1):
          inputstr = " ".join(["INPUT={}".format(x) for x in input])
          shell("{cmd} {ips} OUTPUT={out} {opt}".format(cmd=params.cmd, ips=inputstr, out=output.merge, opt=params.options))
      else:
          if exists(output.merge):
              os.unlink(output.merge)
          shutil.copy(input[0], output.merge)

##############################
# pca rule
##############################
rule scrnaseq_pca:
    input: expr = "{prefix}.csv",
           annotation = config['bio.ngs.settings']['annotation']['transcript_annot_gtf'] if config['bio.ngs.settings']['annotation']['transcript_annot_gtf'] else []
    output: pca = "{prefix}.pca.csv",
            pcaobj = "{prefix}.pcaobj.pickle"
    run:
        scrnaseq_pca(config, input, output)

##############################
# Annotation rule
##############################
def _annotated_data(infile, annotation, **kwargs):
    try:
        from snakemakelib.bio.ngs.rnaseq.utils import read_gene_expression
        df = read_gene_expression(infile, annotation, **kwargs)
        return df
    except:
        raise

rule annotate_expression_data:
    """Annotate expression data"""
    input: expr = "{prefix}{type}.csv",
           annotation = config['bio.ngs.settings']['annotation']['transcript_annot_gtf']
    output: expr = "{prefix}{type,(\.genes|\.isoforms|\.[a-z]+)}.annotated.csv"
    run:
        kwargs = {'unit_id' : "transcript_id" if wildcards.type == ".isoforms" else "gene_id",
                  'unit_name' : "transcript_name" if wildcards.type == ".isoforms" else "gene_name"}
        df = _annotated_data(input.expr, input.annotation, **kwargs)
        with open(output.expr, "w") as fh:
             df.to_csv(fh)


##############################
# Summarization rules
##############################
# We want to make sure summary file matches inputs on label
def _rsem_merge_targets(wildcards):
    return make_targets(tgt_re = config['settings']['sample_organization'].sample_re,
                        samples = _samples,
                        target_suffix = "".join(["", wildcards.label, wildcards.type]) + '.results')

rule rsem_summarize_expression_data:
    input: _rsem_merge_targets
    output: csv = "{prefix}rsem{label}{type,(\.genes|\.isoforms)}.csv"
    run:
        summarize_expression_data(input, output.csv,
                                  config['settings']['sample_organization'].sample_re)

def _rpkmforgenes_merge_targets(wildcards):
    return make_targets(tgt_re = config['settings']['sample_organization'].sample_re,
                        samples = _samples,
                        target_suffix = "." + wildcards.label + '.rpkmforgenes')
        
rule rpkmforgenes_summarize_expression_data:
    input: _rpkmforgenes_merge_targets
    output: csv = "{prefix}rpkmforgenes.{label}.csv"
    run:
        summarize_expression_data(input, output.csv,
                                  config['settings']['sample_organization'].sample_re)


rule save_align_rseqc_data:
    """Save joint alignment and rseqc data"""
    input: align_log = Align.targets['log'],
           rseqc_read_distribution = RSeQC_readDistribution.targets['txt'],
           rseqc_gene_body_coverage = RSeQC_geneBodyCoverage.targets['txt']
    output: csv = join(config['scrnaseq.workflow']['aggregate_output_dir'], "align_rseqc.csv")
    run:
        _save_align_rseqc_metrics(config, output, Align, RSeQC_readDistribution, RSeQC_geneBodyCoverage)


##############################
# QC - create html report
##############################
csv = "{annot}.csv".format(annot=annotationstring)
pcacsv = "{annot}.pca.csv".format(annot=annotationstring)

rule scrnaseq_qc:
    """Perform basic qc on samples. First qc summary report"""
    input: alignqc = join("{path}", "align_rseqc.csv"),
           rpkmforgenes = join("{path}", "rpkmforgenes.merge" + csv) if 'rpkmforgenes' in config['workflows.bio.scrnaseq']['quantification'] else [],
           rpkmforgenespca = join("{path}", "rpkmforgenes.merge" + pcacsv) if 'rpkmforgenes' in config['workflows.bio.scrnaseq']['quantification'] else [],
           rsemgenes = join("{path}", "rsem.merge" + tx + ".genes" + csv) if 'rsem' in config['workflows.bio.scrnaseq']['quantification'] else [],
           rsemisoforms = join("{path}", "rsem.merge" + tx + ".isoforms" + csv) if 'rsem' in config['workflows.bio.scrnaseq']['quantification'] else [],
           rsemgenespca = join("{path}", "rsem.merge" + tx + ".genes" + pcacsv) if 'rsem' in config['workflows.bio.scrnaseq']['quantification'] else [],
           rulegraph = join("{path}", "scrnaseq_all_rulegraph.png"),
           globalconf = join("{path}", "smlconf_global.yaml")
    output: html = join("{path}", "scrnaseq_summary.html")
    run:
        scrnaseq_qc(config, input, output)


rule scrnaseq_qc_new:
    input: results.aggregate_targets['alignrseqc']
    output: html = join(config['scrnaseq.workflow']['report']['directory'], "scrnaseq_summary_new.html")
    run:
        scrnaseq_qc(config, input, output, results=results)
